// Imports

use graffiti::json::JsonImpl;
use starknet::ContractAddress;
use crate::types::reward::{QuestReward, RewardTrait};

// Types

#[derive(Clone, Drop, Serde)]
pub struct QuestMetadata {
    pub name: ByteArray,
    pub description: ByteArray,
    pub icon: ByteArray,
    pub registry: ContractAddress,
    pub rewards: Span<QuestReward>,
}

// Errors

pub mod Errors {
    pub const METADATA_INVALID_NAME: felt252 = 'Metadata: invalid name';
    pub const METADATA_INVALID_DESCRIPTION: felt252 = 'Metadata: invalid description';
    pub const METADATA_INVALID_HIDDEN: felt252 = 'Metadata: invalid hidden';
    pub const METADATA_INVALID_INDEX: felt252 = 'Metadata: invalid index';
    pub const METADATA_INVALID_GROUP: felt252 = 'Metadata: invalid group';
    pub const METADATA_INVALID_ICON: felt252 = 'Metadata: invalid icon';
    pub const METADATA_INVALID_DATA: felt252 = 'Metadata: invalid data';
    pub const METADATA_INVALID_REGISTRY: felt252 = 'Metadata: invalid registry';
}

// Implementations

#[generate_trait]
pub impl QuestMetadataImpl of QuestMetadataTrait {
    #[inline]
    fn new(
        name: ByteArray,
        description: ByteArray,
        icon: ByteArray,
        registry: ContractAddress,
        rewards: Span<QuestReward>,
    ) -> QuestMetadata {
        // [Check] Inputs
        MetadataAssert::assert_valid_name(@name);
        MetadataAssert::assert_valid_description(@description);
        MetadataAssert::assert_valid_icon(@icon);
        MetadataAssert::assert_valid_registry(@registry);
        // [Return] Metadata
        QuestMetadata { name, description, icon, registry, rewards }
    }

    #[inline]
    fn jsonify(mut self: QuestMetadata) -> ByteArray {
        // [Return] Metadata
        let mut rewards: Array<ByteArray> = array![];
        while let Option::Some(reward) = self.rewards.pop_front() {
            rewards.append(reward.clone().jsonify());
        }
        let registry: felt252 = self.registry.into();
        let registry: u256 = registry.into();
        JsonImpl::new()
            .add("name", self.name)
            .add("description", self.description)
            .add("icon", self.icon)
            .add("registry", format!("{}", registry))
            .add_array("rewards", rewards.span())
            .build()
    }
}

#[generate_trait]
pub impl MetadataAssert of AssertTrait {
    #[inline]
    fn assert_valid_name(name: @ByteArray) {
        assert(name.len() > 0, Errors::METADATA_INVALID_NAME);
    }

    #[inline]
    fn assert_valid_description(description: @ByteArray) {
        assert(description.len() > 0, Errors::METADATA_INVALID_DESCRIPTION);
    }

    #[inline]
    fn assert_valid_icon(icon: @ByteArray) {
        assert(icon.len() > 0, Errors::METADATA_INVALID_ICON);
    }

    #[inline]
    fn assert_valid_registry(registry: @ContractAddress) {
        assert(registry != @0.try_into().unwrap(), Errors::METADATA_INVALID_REGISTRY);
    }
}

#[cfg(test)]
mod tests {
    // Local imports

    use super::*;

    // Constants

    const ICON: felt252 = 'ICON';
    const REGISTRY: ContractAddress = 'REGISTRY'.try_into().unwrap();

    #[test]
    fn test_metadata_complete() {
        let name: ByteArray = "NAME";
        let description: ByteArray = "DESCRIPTION";
        let icon: ByteArray = "ICON";
        let rewards: Span<QuestReward> = array![RewardTrait::new("NAME", "DESCRIPTION", "ICON")]
            .span();
        let metadata: ByteArray = QuestMetadataImpl::new(name, description, icon, REGISTRY, rewards)
            .jsonify();
        assert_eq!(
            metadata,
            "{\"name\":\"NAME\",\"description\":\"DESCRIPTION\",\"icon\":\"ICON\",\"rewards\":[{\"name\":\"NAME\",\"description\":\"DESCRIPTION\",\"icon\":\"ICON\"}]}",
        );
    }

    #[test]
    fn test_metadata_empty() {
        let name: ByteArray = "NAME";
        let description: ByteArray = "DESCRIPTION";
        let icon: ByteArray = "ICON";
        let metadata: ByteArray = QuestMetadataImpl::new(
            name, description, icon, REGISTRY, array![].span(),
        )
            .jsonify();
        assert_eq!(
            metadata,
            "{\"name\":\"NAME\",\"description\":\"DESCRIPTION\",\"icon\":\"ICON\",\"rewards\":[]}",
        );
    }

    #[test]
    #[should_panic(expected: ('Metadata: invalid name',))]
    fn test_metadata_creation_new_invalid_name() {
        let description: ByteArray = "DESCRIPTION";
        let icon: ByteArray = "ICON";
        QuestMetadataImpl::new("", description, icon, REGISTRY, array![].span()).jsonify();
    }

    #[test]
    #[should_panic(expected: ('Metadata: invalid description',))]
    fn test_metadata_creation_new_invalid_description() {
        let name: ByteArray = "NAME";
        let icon: ByteArray = "ICON";
        QuestMetadataImpl::new(name, "", icon, REGISTRY, array![].span()).jsonify();
    }

    #[test]
    #[should_panic(expected: ('Metadata: invalid icon',))]
    fn test_metadata_creation_new_invalid_icon() {
        let name: ByteArray = "NAME";
        let description: ByteArray = "DESCRIPTION";
        QuestMetadataImpl::new(name, description, "", REGISTRY, array![].span()).jsonify();
    }
}

